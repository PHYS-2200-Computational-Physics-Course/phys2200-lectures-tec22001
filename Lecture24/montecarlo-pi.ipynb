{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "# Monte Carlo calculations of $\\pi$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "*Monte Carlo methods* are a broad class of computational algorithms that rely \n",
    "on repeated random sampling to obtain numerical results. The underlying concept \n",
    "is to use randomness to solve deterministic problems."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "As an illustration of the idea, let's\n",
    "consider the following Monte Carlo algorithm for calculating the\n",
    "value of $\\pi$:"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "> A quarter of a circle of radius $r = 1$ (with the area $A_c = \\frac{\\pi}{4}$)\n",
    "> is inscribed in a square with the side length $l = 1$ (with the area $A_s = 1$),\n",
    "> as shown in the first plot below. We generate uniformly distributed random points\n",
    "> inside the unit square, that is we generate pars of uniformly distributed random\n",
    "> numbers, $x$ and $y$, such that $0 \\le x, y \\le 1$. We count the fraction, $\\nu$,\n",
    "> of random points that lands inside the circle, that is such points where $x^2 + y^2 \\le 1$.\n",
    "> Since the number of uniformly distributed random points inside any shape is proportianal\n",
    "> to its area, the fractions of of points inside the quater circle $$\\nu = \\frac{A_c}{A_s} = \\frac{\\pi}{4}.$$\n",
    "> Thus, calculating $\\nu$, we get the following estimate for $\\pi$: $$pi = 4 \\nu. $$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "In this notes we implement the Monte Carlo algorithm described above and investigate its convergence properties."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### **0**. \n",
    "\n",
    "Load the required packages."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using Random: seed!           # only load the function to seed a random number generator\n",
    "using PyPlot\n",
    "using Statistics: mean, std   # only load the functions to calculate the mean and the standard deviation"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Sketch the setup:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "phi = range(0.0, pi/2, 50)\n",
    "plot(cos.(phi), sin.(phi), color=\"black\")\n",
    "plot([0.0, 1.0, 1.0, 0.0, 0.0], [0.0, 0.0, 1.0, 1.0, 0.0], color=\"black\")\n",
    "seed = 1234\n",
    "seed!(seed)\n",
    "for i = 1:20\n",
    "    x, y = rand(2)\n",
    "    if x^2 + y^2 < 1\n",
    "        mar = \"o\"\n",
    "        col = \"blue\"\n",
    "    else\n",
    "        mar = \"x\"\n",
    "        col = \"red\"\n",
    "    end\n",
    "    scatter(x, y, marker=mar, color=col)\n",
    "end\n",
    "axis(\"off\")\n",
    "axis(\"square\");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### **1**. \n",
    "\n",
    "Write a Julia function, `mcpi`,  that uses the algorithm described above, accepts a number of Monte Carlo steps $m$, and returns the estimate of `pi`"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\"\"\"\"\n",
    "    pi_est = mcpi(m)\n",
    "\n",
    "Monte Carlo calculation of pi\n",
    "\"\"\"\n",
    "function mcpi(m)\n",
    "    s = 0\n",
    "    for i = 1:m\n",
    "        x, y = rand(2)\n",
    "        if x^2 + y^2 <= 1.0\n",
    "            s += 1\n",
    "        end\n",
    "    end\n",
    "    return 4*s/m\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "jp-MarkdownHeadingCollapsed": true
   },
   "source": [
    "\n",
    "#### **2**. \n",
    "\n",
    "The plan of the numerical experiments:\n",
    "\n",
    "Run multiple Monte Carlo experiments calculating $\\pi$. In each subsequent experiment\n",
    "double the number of the Monte Carlo steps.\n",
    "For a selected number of Monte Carlo steps, \n",
    "repeat the calculations `k` times, obtaining `k` (slightly) different results for $\\pi$.\n",
    "Use the mean of those results as an estimation for $\\pi$.\n",
    "Use the standard deviation of the results as an estimation of the error of Monte Carlo calculations. "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### **3**. \n",
    "\n",
    "Initialize the default random number generator, so that the calculation can be reproduced\n",
    "\n",
    "Choose your own seed."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "seed = 1\n",
    "seed!(seed)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### **4**. \n",
    "\n",
    "Assign parameters and prealocate working arrays"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "n = 12                  # the number of experiments\n",
    "nps = zeros(Int64, n)   # the numbers of MC steps in each experiment\n",
    "pis = zeros(n)          # the estimations of `pi` obtained in each experiment \n",
    "pistds = zeros(n);      # the stds of estimations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "k = 256               # the number of MC runs with a fixed number of MC steps\n",
    "res = zeros(k)        # storage for the results of `k' MC runs\n",
    "ns = zeros(Int64, k); # working array of integers"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### **5**. \n",
    "\n",
    "Run the calculations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "@time for i = 1:n\n",
    "    nps[i] = 2^(i+10)\n",
    "    @info i, nps[i]\n",
    "    fill!(ns, nps[i])  # fill the array `ns` with values of np[i]\n",
    "    res .= mcpi.(ns)   # run mce() k times and store the results to array `res`\n",
    "    pis[i] = mean(res)\n",
    "    pistds[i] = std(res, mean=pis[i])/sqrt(k)  # the stds\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Print the estimates of pi in all your calculations:"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Let's look at the results of the very last experiment:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "hist(res)\n",
    "grid(true)\n",
    "title(\"Distribution of the result of a Monte Carlo run\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "pis"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Plot in the same figure the absolute errors of the results of Monte Carlo calculations, \n",
    "`abs.(pis .- pi)`, and the standard deviations of the results, `pistds`, vs. the number\n",
    "of Monte Carlo steps.\n",
    "\n",
    "Chose a proper type of your plot (linear, semilog, loglog, etc) so that you can deduce \n",
    "the relations between errors and the number of MC steps. Provide grid, axes labels, legend, and title."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "loglog(nps, abs.(pis .- pi), label=\"True errors\", marker=\"o\", markersize=2, linestyle=\"none\")\n",
    "loglog(nps, pistds, label=\"Stds\", marker=\"o\", markersize=3, linestyle=\"none\")\n",
    "ylim(1e-6, 1e-1)\n",
    "grid(true)\n",
    "xlabel(\"MC steps\")\n",
    "ylabel(\"Absolute error\")\n",
    "title(\"Results of Monte Carlo calculations\")\n",
    "legend();"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "The graph provides convincing arguments that the standard deviations of the results of Monte Carlo runs\n",
    "can serve as an (over)estimation of the true absolute errors. The latter are never known for 'real-life' \n",
    "calculations whereas the former is a part of the results of the calculations."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### **6**. \n",
    "\n",
    "Find the dependence: standard deviations of the results of Monte Carlo runs vs the number of Monte Carlo steps\n",
    "\n",
    "The last plot suggests a power law for the dependence of standar deviation vs the number of MC steps, $std \\sim N^\\beta$.\n",
    "Use linear regression to find $\\beta$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "function linear_regression(x, y)\n",
    "    np = length(x)\n",
    "    xbar = sum(x)/np\n",
    "    ybar = sum(y)/np\n",
    "    x2 = sum((x .- xbar) .^ 2)\n",
    "    beta = sum((y .- ybar) .* (x .- xbar))/x2\n",
    "    alpha = ybar - beta*xbar\n",
    "    sigma = sqrt(sum((y .- alpha .- beta .* x) .^ 2)/((np - 2)*x2))\n",
    "    return alpha, beta, sigma\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "alpha, beta, sigma = linear_regression(log.(nps), log.(pistds));"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Print the results, rownding to a meaningful value of significant digits:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "round(beta, sigdigits=2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "round(sigma, sigdigits=1)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### **7**. \n",
    "\n",
    "Plot the results of MC simulations."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "loglog(nps, abs.(pis .- pi), label=\"True error\", marker=\"o\", markersize=2, linestyle=\"none\")\n",
    "loglog(nps, pistds, label=\"Std\", marker=\"o\", markersize=3, linestyle=\"none\")\n",
    "loglog(nps, exp.(alpha .+ beta .* log.(nps)), label=\"LSq fit std\", linestyle=\"dashed\")\n",
    "grid(true)\n",
    "xlabel(\"MC steps\")\n",
    "ylabel(\"Absolute error\")\n",
    "title(\"MC simulations results and linear regression fit\")\n",
    "legend();"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### **8**. \n",
    "\n",
    "Conclusions\n",
    "\n",
    "\n",
    "The graph demonstrates that \n",
    "\n",
    "$$s \\sim \\frac{1}{\\sqrt{N_{mc}}} ,$$\n",
    "\n",
    "where $N_{mc}$ is the number of Monte Carlo steps. That is the dependence that is typical for MC simulations."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
