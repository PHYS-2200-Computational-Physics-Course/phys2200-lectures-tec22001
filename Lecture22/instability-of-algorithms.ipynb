{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "# Instability of algorithms\n",
    "\n",
    "In this notes we discuss an example of a *well-posed* problem that we try \n",
    "to solve using an *unstable algorithm*, thus geting a poor answer.\n",
    "(We also correct the difficulty by changing the algorithm.)\n",
    "\n",
    "However, for certain problems, \"good\" answers cannot be obtained by \n",
    "any algorithm because the problem is sensitive to the small errors \n",
    "made in data representation and arithmetic. \n",
    "\n",
    "It is important to distinguish between these two classes of pitfalls because\n",
    "there are unstable algorithms and sensitive problems in nearly all branches\n",
    "of numerical mathematics. Once you become aware of their symptoms, these\n",
    "problems are fairly easy to diagnose."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Suppose we wish to compute the integrals \n",
    "\n",
    "$$I(n) = \\int_0^1 x^n e^{-x} \\mathrm{d}x , \\quad n = 1, 2, \\ldots \\tag{1}$$\n",
    "\n",
    "Integrals $I(n)$ have the following properties:\n",
    "\n",
    "- All $I(n)$ are strictly positive, $I(n) > 0$, since the integrand is positive throughout the interval (0, 1). \n",
    "\n",
    "- $I(n)$ are monotonically decreasing as n is increasing: \n",
    "since $x^{n+1} < x^n$ for $0 < x < 1$, then $x^{n+1} e^{-x} < x^n e^{-x}$, \n",
    "and thus $I(n+1) < I(n)$.\n",
    "\n",
    "We'll use those properties of $I(n)$ to check the validity of our numerical calculations."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Integrating Eq. (1) by parts, \n",
    "\n",
    "$$\n",
    "I(n+1) = -\\int_0^1 x^{n+1} \\mathrm{d}(e^{-x}) =  \n",
    "- \\left. x^{n+1} e^{-x} \\right|_0^1 + \\int_0^1 e^{-x} \\mathrm{d}(x^{n+1}) =\n",
    "(n+1) \\int_0^1 x^n e^{-x} \\mathrm{d}x  - \\frac{1}{e} =\n",
    "(n+1) I(n) - \\frac{1}{e} ,\n",
    "$$\n",
    "\n",
    "we establish the following recurrence relation:\n",
    "\n",
    "$$I(n+1) = (n+1) I(n) - \\frac{1}{e}. \\tag{2}$$\n",
    "\n",
    "In addition, we know the value of the elementary integral $I(1)$:\n",
    "\n",
    "$$I(1) = 1 - \\frac{2}{e} . \\tag{3}$$\n",
    "\n",
    "We can use recurrence relation Eq. (2) together with the initial condition Eq. (3) to calculate $I(n)$ for any $n$."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "We are going to calculate the integrals $I(n)$ \n",
    "using just described recurrence algorithm. We check the accuracy \n",
    "of the calculations by comparing the values of the integrals with the ones \n",
    "obtained by the function `quadgk` from the Julia package `QuadGK`."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### 0. \n",
    "\n",
    "Load required packages. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "using QuadGK\n",
    "using PyPlot"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Select the number of integrals to calculate:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "np = 20"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Define a helper function for the integrand:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "integrand(x, n) = x^n * exp(-x)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### 1. \n",
    "\n",
    "Write a function, `quadrature(m)`, that calculates the integrals I(n) \n",
    "for $n = 1, 2, ..., m$ using `quadgk` integrator and \n",
    "returns a vector of calculated integrals' values."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\"\"\"\n",
    "     res = quadrature(m)\n",
    "\n",
    "Evaluate the integrals I(n), Eq. (1) for n = 1:m using quadgk integrator. \n",
    "Return a vector of calculated integrals' values.\n",
    "\"\"\"\n",
    "function quadrature(m)\n",
    "    r = zeros(m)\n",
    "    for n = 1:m\n",
    "        r[n] = quadgk(x -> integrand(x, n), 0.0, 1.0)[1]\n",
    "    end\n",
    "    return r\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Calculate the integrals using `quadrature` function:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "res_q = quadrature(np);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### 2.\n",
    "\n",
    "Write a function, `recurrence(m)`, that calculates the integrals I(n) \n",
    "for $n = 1, 2, ..., m$ using the recurrence relations Eq. (2) and \n",
    "returns a vector of calculated integrals' values."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\"\"\"\n",
    "     res = recurrence(m)\n",
    "\n",
    "Evaluate the integrals I(n), Eq. (1) for n = 1:m using recurrence relations Eq. (2). \n",
    "Return a vector of calculated integrals' values.\n",
    "\"\"\"\n",
    "function recurrence(m)\n",
    "    r = zeros(m)\n",
    "    r[1] = 1.0 - 2*exp(-1.0)\n",
    "    for n = 1:(m-1)\n",
    "        r[n+1] = (n + 1) * r[n] - exp(-1.0)\n",
    "    end\n",
    "    return r\n",
    "end "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Calculate the integrals using `recurrence` function:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "res_r = recurrence(np);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### 3.\n",
    "\n",
    "In the same figure, plot the results calculated by both functions.  Provide the grid, axes labels, title, and the legend for your graph."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(1:np, res_q, marker=\"o\", label=\"QuadGK\")\n",
    "plot(1:np, res_r, marker=\"o\", label=\"Recurrence\")\n",
    "ylim(-0.25, 0.3)\n",
    "legend()\n",
    "grid(true)\n",
    "title(\"I(n) calculated using two different methods\")\n",
    "xlabel(\"n\")\n",
    "ylabel(\"I(n)\");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Something is obviously wrong: the values of $I(n)$ calculate by recurrence algorithm are not positive! "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### 4.\n",
    "\n",
    "To see what is exactly wrong, plot the absolute error of evaluation of $I(n)$, \n",
    "assuming that `quadgk` integrator produces accurate results. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "semilogy(2:np, (abs.(res_q .- res_r))[2:np], marker=\"o\", label=\"numerical experiment\", linestyle=\"none\")\n",
    "semilogy(2:np, eps() * factorial.(2:np), linestyle=\"dashed\", label=L\"\\epsilon \\, n!\")\n",
    "grid(true)\n",
    "legend()\n",
    "xlim(2,20)\n",
    "xlabel(\"n\")\n",
    "ylabel(\"absolute error\")\n",
    "title(\"Factorial growth of the absolute error\");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "We the that the absolute error in $I(n)$ grows as $n$ factorial, $n!$. \n",
    "What caused the large error? Observe that the only roundoff error made in\n",
    "the above calculations was in $I(1)$ where $1 - \\frac{2}{e}$ was rounded \n",
    "introducing an error of the order of machine epsilon. To see how the error \n",
    "of about $10^{-16}$ in $I(1)$ becomes so large,\n",
    "notice that it is multiplied by $2$ in the calculation of $I(2)$, then the error in\n",
    "$I(2)$ is multiplied by $3$ in computing $I(3)$, and so on. Thus, the error in $I(20)$\n",
    "is exactly the error in $I(1)$ multiplied by $2 \\times 3 \\times 4 \\ldots \\times 20 = 20!$ or about $2.4 \\times 10^{18}$.\n",
    "This enormous magnification of the error is a result of the algorithm we choose to use."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "#### 5.\n",
    "\n",
    "How can we modify the algorithm to avoids this instability?\n",
    "If we rewrite the recurrence relation Eq. (2) as\n",
    "\n",
    "$$I(n) = \\frac{I(n+1) + \\frac{1}{e}}{n+1}, \\qquad n = \\ldots, 4, 3, 2, 1,$$\n",
    "\n",
    "then at each stage of the calculation, the error in $I(n)$ is decreased by a factor\n",
    "of 1/(n+1). So if we start with a value for some $I(k)$ and work backbackwards, \n",
    "any initial error or rounding errors which occur will be decreased at\n",
    "each step. This is called a *stable algorithm*."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\"\"\"\n",
    "     res = recurrence_reverse(m, extra)\n",
    "\n",
    "Evaluate the integrals I(n), Eq. (1) for n = 1:(m+extra) using the \"reverse\"\n",
    "recurrence relation I(n) = (I(n+1) - 1/e)/(n+1), with the initial condition \n",
    "I(m+extra) = 0.\n",
    "Return a vector of calculated integrals' values discarding the last `extra` elements\n",
    "\"\"\"\n",
    "function recurrence_reverse(m, mextra)\n",
    "    r = zeros(m+mextra)\n",
    "    r[end] = 0.0\n",
    "    for n = (m+mextra-1):-1:1\n",
    "        r[n] = (r[n+1] + exp(-1))/(n+1)\n",
    "    end\n",
    "    return r[1:m]  # discard the last mextra elements\n",
    "end "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Calculate the integrals using `recurrence_reverse` function:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "res_rr = recurrence_reverse(np, 11);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Plot the absolute error of evaluation of $I(n)$ using the reverse recurrence: "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "plot(1:np, (abs.(res_q .- res_rr)), marker=\"o\", label=\"numerical experiment\", linestyle=\"none\")\n",
    "grid(true)\n",
    "xlim(2,20)\n",
    "xlabel(\"n\")\n",
    "ylabel(\"absolute error\")\n",
    "title(\"Improved recurrence algorithm\");"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "Notice that errors now are of order of machine precision which is the best we can achieve in numerical calculations. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
